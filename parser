#!/usr/bin/awk -f

function inline(s) {
	gsub(/\\/, "\\\\", s)
	gsub(/<</, "\\(Fo", s)
	gsub(/>>/, "\\(Fc", s)
	gsub(/\s---/, "\\h'4p'\\(em", s)
	gsub(/--/, "\\(em", s)
	gsub(/''''/, "\\f(BI", s)
	gsub(/'''/, "\\fI", s)
	gsub(/''/, "\\fB", s)
	gsub(/\$\$/, "\\fP", s)
	s = gensub(/\s*\$\$([^$]+)\$\$\s*/, "\n.EQ\n\\1\n.EN\n", "g", s)
	sub(/^\n/, "", s)
	sub(/\n$/, "", s)

	return s
}

/^-\ .+/ {
	sub(/^-\ /, "")
	file = file ".heading@S1 \"" inline($0) "\"\n"
	content = content ".CO"
	next
}

/^--\ .+/ {
	sub(/^--\ /, "")
	file = file ".heading@S2 \"" inline($0) "\"\n"
	next
}

/^---\ .+/ {
	sub(/^---\ /, "")
	file = file ".heading@S3 \"" inline($0) "\"\n"
	next
}

/^-$/ {
	file = file ".font@monospace\n"

	for (; (getline s) > 0;) {
		if (match(s, /^-$/))
			break

		gsub(/\\/, "\\\\", s)
		file = file s "\n"
	}

	file = file ".font@multispace\n"

	next
}

/^--$/ {
	if (isrs) {
		file = file ".list@end\n"
		isrs = 0
	}
	else {
		file = file ".list@start\n"
		isrs = 1
	}
	next
}

/^---$/ {
	file = file ".br\n"
	next
}

/^===$/ {
	file = file ".bp\n"
	getline
	next
}

/^$/ {
	file = file ".sp\n"
	next
}

/^\ {4}/ {
	sub(/^\ {4}/, "")
	file = file ".paragraph\n" inline($0) "\n"
	next
}

/^\t{1,3}\S+\ .+/ {
	if (match($0, /^\t{3}\S+\ /)) {
		file = file ".list-item@L3 "
		num = substr($0, 4, RLENGTH - 4)
	}
	else if (match($0, /^\t{2}\S+\ /)) {
		file = file ".list-item@L2 "
		num = substr($0, 3, RLENGTH - 3)
	}
	else if (match($0, /^\t\S+\ /)) {
		file = file ".list-item@L1 "
		num = substr($0, 2, RLENGTH - 2)
	}

	sub(/^\t{1,3}\S+\ /, "")

	if (num == "*")
		num = "\\(bu"

	file = file num "\n" inline($0) "\n"
	next
}

/^!P/ {
	if (match($0, /^!P\S+$/)) {
		file = file ".image 0 " substr($0, 3) "\n"
		next
	}

	match($0, /\S+$/)
	var = substr($0, RSTART, RLENGTH)
	vars[var] = ++picnum

	file = file gensub(/^!P(\S+)\ (.+)\ \S+$/, ".image 1 \\1 " picnum " \"\\2\"\n", "g")
	next
}

/^!T/ {
	if (match($0, /^!T\S+$/)) {
		file = file ".table 0 " substr($0, 3) "\n"
		next
	}

	match($0, /\S+$/)
	var = substr($0, RSTART, RLENGTH)
	vars[var] = ++tblnum

	file = file gensub(/^!T(\S+)\ (.+)\ \S+$/, ".table 1 \\1 " tblnum " \"\\2\"\n", "g")
	next
}

/^\$\$\ \S+/ {
	sub(/^\$\$\ /, "")
	vars[$0] = ++exprnum

	file = file ".ce 1000\n.mk es\n.EQ\n"

	while (getline > 0) {
		if ($0 ~ /^\$\$$/) {
			break
		}

		file = file $0 "\n"
	}

	file = file ".EN\n.mk en\n"
	file = file ".sp -((\\n[en]u-\\n[es]u)/2u+0.5v)\n"
	file = file ".in 0\n.ta \\n(.luR\n"
	file = file "\t(" exprnum ")\n"
	file = file ".sp |\\n[en]u\n"
	file = file ".ce 0\n"

	next
}

{
	file = file inline($0) "\n"
}

END {
	for (id in vars) {
		reg = "\\*" id "\\*"
		gsub(reg , vars[id], file)
	}

	print file
}
